dnl Process this file with autoconf to produce a configure script.
dnl
dnl (C)Copyright 2001 by Hiroshi Takekawa
dnl This file is part of multiskkserv.
dnl
dnl Last Modified: Tue Mar 13 13:06:33 2001.
dnl $Id$
dnl
dnl This software is free software; you can redistribute it and/or modify it
dnl under the terms of the GNU General Public License version 2 as
dnl published by the Free Software Foundation.
dnl
dnl This software is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA

dnl AC_INIT
dnl AC_CONFIG_SRCDIR([src/multiskkserv.c])
AC_INIT(src/multiskkserv.c)

AM_INIT_AUTOMAKE(multiskkserv, 20010313)
AM_CONFIG_HEADER(config.h)
AM_PROG_LIBTOOL

# Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S

# Checks for libraries.
available_pthread=no
AC_CHECK_HEADER(pthread.h, [
  AC_CHECK_LIB(pthread, pthread_create, [ available_pthread=yes; LIBADD_PTHREAD=-lpthread; ], [
    AC_CHECK_LIB(c_r, pthread_create, [ available_pthread=yes; LIBADD_PTHREAD=-lc_r; ], [
      AC_CHECK_FUNC(pthread_create, [ available_pthread=yes; ]) ]) ]) ])
if test "$available_pthread" = "yes"; then
  AC_DEFINE(USE_PTHREAD,,[ Define if you have libpthread ])
  CFLAGS="$CFLAGS -D_REENTRANT"
fi
AM_CONDITIONAL(HAVE_PTHREAD, test "$available_pthread" = "yes")
AC_SUBST(LIBADD_PTHREAD)

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h netdb.h netinet/in.h stdlib.h string.h sys/socket.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE

# Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memset socket strchr strdup])

available_getaddrinfo=no
AC_CHECK_FUNC(getaddrinfo, [ available_getaddrinfo=yes; AC_DEFINE(HAVE_GETADDRINFO, 1, [Define if you have the 'getaddrinfo' function.]) ])
AM_CONDITIONAL(HAVE_GETADDRINFO, test "$available_getaddrinfo" = "yes")

available_getnameinfo=no
AC_CHECK_FUNC(getnameinfo, [ available_getnameinfo=yes; AC_DEFINE(HAVE_GETNAMEINFO, 1, [Define if you have the 'getnameinfo' function.]) ])
dnl AM_CONDITIONAL(HAVE_GETNAMEINFO, test "$available_getnameinfo" = "yes")

# Options
dnl debug option
AC_ARG_ENABLE(debug,
        [  --enable-debug          build a debugging version.],
        [ enable_debug=$enableval ],
        [ enable_debug=no ])
test "$enable_debug" = yes && AC_DEFINE(DEBUG, 1, [Define if you want to debug.]
)

CDB_DIR=""
AC_ARG_WITH(cdb,
  [  --with-cdb              specify cdb path (or auto).],
  [ with_cdb=$withval ],
  [ with_cdb=yes ])
if test "$with_cdb" = "yes"; then
  for i in /usr/local /usr; do
    if test -d "$i/include" -a -f "$i/include/cdb.h"; then
      CDB_DIR="-I$i/include"
      for j in cdb.a buffer.a unix.a byte.a alloc.a; do
        if test -f "$i/lib/$j"; then
          LIBADD_CDB="$LIBADD_CDB $i/lib/$j"
        else
          echo "Incomplete build."
        fi
      done
    fi
  done
else
  if test -d "$with_cdb" -a -f "$with_cdb/cdb.h"; then
    CDB_DIR="-I$with_cdb"
    for j in cdb.a buffer.a unix.a byte.a alloc.a; do
      if test -f "$with_cdb/$j"; then
        LIBADD_CDB="$LIBADD_CDB $with_cdb/$j"
      fi
    done
  fi
fi
if test -z "$CDB_DIR"; then
  echo "FATAL: cdb not found."
fi
AC_SUBST(CDB_DIR)
AC_SUBST(LIBADD_CDB)

dnl CFLAGS="$CFLAGS -Werror -Wstrict-prototypes -Wwrite-strings"
CFLAGS="$CFLAGS -Wstrict-prototypes -Wwrite-strings"

dnl AC_CONFIG_FILES([Makefile lib/Makefile src/Makefile tools/Makefile])
dnl AC_OUTPUT
AC_OUTPUT(Makefile lib/Makefile src/Makefile tools/Makefile)
